*> Advent of Code 2017 Day 2 Challenge 2
IDENTIFICATION DIVISION.
PROGRAM-ID. DAY2_1.

ENVIRONMENT DIVISION.
INPUT-OUTPUT SECTION.
FILE-CONTROL.
SELECT IN-FILE 
    ASSIGN TO './utils/INPUT.DAT'
    ORGANIZATION IS LINE SEQUENTIAL.

DATA DIVISION.
    FILE SECTION.
        FD IN-FILE.
        01 IN-ROW               PIC A(64).
    WORKING-STORAGE SECTION.
        01 WS-NUM-ROWS          PIC 9(2) VALUE 16.
        01 WS-NUM-COLS          PIC 9(2) VALUE 15.
        01 WS-INDEXES.
            05 WS-ROW           PIC 9(2).
            05 WS-COL-1         PIC 9(2).
            05 WS-COL-2         PIC 9(2).
            05 WS-START-COL     PIC 9(2).
        01 WS-NUMS.
            05 WS-NUM-1         PIC 9(4).
            05 WS-NUM-2         PIC 9(4).
        01 WS-DIVISION.
            05 WS-QUOTIENT      PIC 9(4).
            05 WS-REMAINDER     PIC 9(4).
        01 WS-ANS               PIC 9(10).
        01 WS-INPUT-TABLE.
            05 WS-INPUT-ROW OCCURS 16 TIMES.
                10 WS-INPUT-NUMBER OCCURS 16 TIMES PIC 9(4).

PROCEDURE DIVISION.
100-MAIN.
    PERFORM 200-CREATE-TABLE THRU 200-CREATE-TABLE-EXIT
    PERFORM 300-FIND-ANS THRU 300-FIND-ANS-EXIT
    DISPLAY WS-ANS
    STOP RUN.

200-CREATE-TABLE.
    OPEN INPUT IN-FILE
        PERFORM VARYING WS-ROW FROM 1 BY 1 UNTIL WS-ROW > WS-NUM-ROWS
            READ IN-FILE
                NOT AT END MOVE IN-ROW TO WS-INPUT-ROW(WS-ROW)
            END-READ
        END-PERFORM.
    CLOSE IN-FILE.

200-CREATE-TABLE-EXIT.
    EXIT.

300-FIND-ANS.
    PERFORM VARYING WS-ROW FROM 1 BY 1 UNTIL WS-ROW > WS-NUM-ROWS
        PERFORM VARYING WS-COL-1 FROM 1 BY 1 UNTIL WS-COL-1 > WS-NUM-COLS
            ADD 1 TO WS-COL-1 GIVING WS-START-COL
            SET WS-NUM-1 TO WS-INPUT-NUMBER(WS-ROW, WS-COL-1)
            PERFORM 400-FIND-DIVISIBLE-NUMS THRU 400-FIND-DIVISIBLE-NUMS-EXIT
        END-PERFORM
    END-PERFORM.  

300-FIND-ANS-EXIT.
    EXIT.

400-FIND-DIVISIBLE-NUMS.
    PERFORM VARYING WS-COL-2 FROM WS-START-COL BY 1 UNTIL WS-COL-2 > WS-NUM-ROWS
        SET WS-NUM-2 TO WS-INPUT-NUMBER(WS-ROW, WS-COL-2)
        DIVIDE WS-NUM-1 BY WS-NUM-2 GIVING
            WS-QUOTIENT REMAINDER WS-REMAINDER
        IF WS-REMAINDER IS EQUAL TO 0
            ADD WS-QUOTIENT TO WS-ANS
        ELSE
            DIVIDE WS-NUM-2 BY WS-NUM-1 GIVING
                WS-QUOTIENT REMAINDER WS-REMAINDER
            IF WS-REMAINDER IS EQUAL TO 0
                ADD WS-QUOTIENT TO WS-ANS
            END-IF
        END-IF
    END-PERFORM.

400-FIND-DIVISIBLE-NUMS-EXIT.
    EXIT.
